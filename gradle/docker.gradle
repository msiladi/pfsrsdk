buildscript {
    apply from: "$rootProject.projectDir/gradle/buildscript.gradle", to: buildscript
}

import com.coreinformatics.GitUtil
import com.coreinformatics.common.property.PropertyUtil

apply plugin: 'com.coreinformatics.docker-plugin'

String getGitBranchName() {
    return project.hasProperty('branchName') ? branchName : grgit.branch.getCurrent().getName()
}

ext {
    // Variant of version string since Docker tags may not contain '+' characters
    String tag = project.version.toString().replaceAll('\\+', '-')

    imageTag = tag
    versionTag = project.ext.packageVersion
    isMaster = GitUtil.isMasterBranch(release.grgit)
    tagsList = [versionTag, 'latest']
    pushable = project.hasProperty('pushable') ? pushable.toBoolean() : GitUtil.isAcceptableBranch(release.grgit, pushableBranches.tokenize(','))
    branchName = getGitBranchName().toLowerCase().replace('/', '-').replace('_', '-')
}

docker {
    url = (project.hasProperty('dockerRestUrl') ? project.property('dockerRestUrl') : PropertyUtil.getPropertyOrEnvironmentValue('dockerRestUrl', 'DOCKER_HOST', ''))
    certPath = new File(project.hasProperty('dockerCertPath') ? project.property('dockerCertPath') : PropertyUtil.getPropertyOrEnvironmentValue('dockerCertPath', 'DOCKER_CERT_PATH', ''))
    logger.info 'docker.url: ' + url
    logger.info 'docker.certPath: ' + certPath

    registryCredentials {
        url = dockerRepository
        username = project.hasProperty('dockerRepositoryUsername') ? project.property('dockerRepositoryUsername') : null
        password = project.hasProperty('dockerRepositoryPassword') ? project.property('dockerRepositoryPassword') : null
        email = project.hasProperty('dockerRepositoryEmail') ? project.property('dockerRepositoryEmail') : null
    }
}

images {
    rOdataSdkDocs {
        containerDirectory = 'docs'
        isMaster = project.ext.isMaster
        dstImageName = dockerDocsImageName
        tags = project.ext.tagsList
        isPushable = project.ext.pushable
        branchName = project.ext.branchName.toLowerCase().replace('/', '-').replace('_', '-')
        fromImage = "${dockerDocsSrcImage}"
    }
    rOdataSdkPackage {
        containerDirectory = 'pkg'
        isMaster = project.ext.isMaster
        dstImageName = dockerPkgImageName
        tags = project.ext.tagsList
        isPushable = project.ext.pushable
        branchName = project.ext.branchName.toLowerCase().replace('/', '-').replace('_', '-')
        fromImage = "${dockerPkgSrcImage}"
    }
}

createROdataSdkDocsConfigfile {
    dependsOn 'pkgdownSite'
    
    destFile = project.file('build/docs/Dockerfile')
   
    def labels = [:]
    labels.put('maintainer', projectVendor)
    labels.put('description', projectDescription)
    label labels
    
    addFile '.', '/usr/share/nginx/html/'
}

createROdataSdkPackageConfigfile {
    dependsOn 'copyPackage'
    
    destFile = project.file('build/pkg/Dockerfile')
   
    def labels = [:]
    labels.put('maintainer', projectVendor)
    labels.put('description', projectDescription)
    label labels
    
    addFile '.', '/R'
    workingDir '/R'
    
    runCommand "R CMD INSTALL ${project.ext.packageName}_${project.ext.packageVersion}.tar.gz"
}

task copyPackage(type: Copy, group: 'docker') {
    dependsOn 'buildPackage'
    
    from 'build'
    into 'build/pkg'
    include '*.tar.gz'
}
